class MAIN_BOSS
{
	//flag	0:start , 1:normal , 2:end
	var t,ct,loop;
	var flag,state;
	var tx,ty;
	MAIN_BOSS()
	{
		t=0;flag=0;state=0;loop=0;ct=0;
		tx=-50;ty=0;
		img=10; hp=2000;
	}
	main()
	{
		t+=1;
		ct+=1;
		if(flag==0)	//apear
		{
			x-=3;
			if(t>20)
			{flag=1;t=0;}
		}
		else if(flag==1)	//atack1 "wing"
		{
			if(state==0)
			{
				if(t>120)
				{state=1;t=0;}
				if(hp<1500)
				{flag=2;t=0;loop=0;state=0;}
			}
			else if(state==1)
			{
				tx-=4;
				if(t>40)
				{state=2;t=0;}
			}
			else if(state==2)
			{
				if(t>80)
				{state=3;t=0;}
			}
			else if(state==3)
			{
				var i;
				tx+=4;
				if(1)
				{
					for(i=0;i<t/10;i+=1)
					{
						Shot(100,
							x+tx+20,
							y+ty+40,
							cos((t-i*10)/(40-i*10)*3.14),
							+1*sin((t-i*10)/(40-i*10)*3.14),
							0);
						Shot(100,
							x+tx+20,
							y+ty+40,
							cos((t-i*10)/(40-i*10)*3.14),
							-1*sin((t-i*10)/(40-i*10)*3.14),
							0);
					}
				}
				if(t>40)
				{state=4;t=0;}
			}
			else if(state==4)
			{
				if(t==20 && loop>0)
				{
					var i,j,k;
					k=atan2(py+18-(y+ty+40),px+16-(x+tx+20));
					for(i=0;i<6;i+=1)
					{
						for(j=2;j<4;j+=0.5)
						{
							Shot(105,
								x+tx+20,
								y+ty+40,
								j*cos(k+i/6*6.28),
								j*sin(k+i/6*6.28),
								0);
						}
					}
				}
				if(t>80)
				{state=1;t=0;loop+=1;}
				if(hp<1500)
				{flag=2;t=0;loop=0;state=0;}
			}
		}
		else if(flag==2)
		{
			tx-=0.5;
			if(t>60)
			{flag=3;t=0;}
		}
		else if(flag==3)	//atack1 "round and shot!"
		{
			tx+=8*sin(ct/30*6.28);
			ty+=8*cos(ct/30*6.28);
			if(state==0)
			{
				if(t>90)
				{state=1;t=0;}
			}
			else if(state==1)
			{
				ty-=2;
				if(t>40)
				{state=2;t=0;}
			}
			else if(state==2)
			{
				ty+=2;
				if(t>60)
				{state=3;t=0;}
				var i,k;
				k=atan2(py+18-(y+ty+40),px+16-(x+tx+20));
				for(i=-4;i<=4;i+=1)
					Shot(101,
						x+tx+20,
						y+ty+40,
						cos(k+i/4*6.28),
						sin(k+i/4*6.28),
						0);
			}
			else if(state==3)
			{
				if(t>30)
				{state=4;t=0;}
			}
			else if(state==4)
			{
				ty-=2;
				if(t>60)
				{state=5;t=0;}
				var i,k;
				k=atan2(py+18-(y+ty+40),px+16-(x+tx+20));
				for(i=-4.5;i<=4.5;i+=1)
					Shot(102,
						x+tx+20,
						y+ty+40,
						2*cos(k+i/4*6.28),
						2*sin(k+i/4*6.28),
						0);
			}
			else if(state==5)
			{
				if(t>30)
				{state=2;t=0;}
			}
			if(hp<1000)
			{flag=4;t=0;loop=0;state=0;}
		}
		else if(flag==4)
		{
			tx+=(-50-tx)/60;ty-=ty/60;
			if(t>120)
			{flag=5;t=0;}
		}
		else if(flag==5)
		{
			if(state==0)
			{
				if(t>60)
				{state=1;t=0;}
			}
			else if(state==1)
			{
				ty=random(0,240)-y-40;
				Shot(100,
					x+tx+20,
					y+ty+40,
					-100/(240-t),
					0,
					0);
				if(t>60)
				{state=2;t=0;}
			}
			else if(state==2)
			{
				if(t>180)
				{state=3;t=0;}
			}
			else if(state==3)
			{
				if(t%120==0)
					ty=random(0,240)-y-40;
				else if(t%120==60)
				{
					var i;
					for(i=0;i<36;i+=1)
					{
						Shot(101,
							x+tx+20,
							y+ty+40,
							(-160+60*cos(i/36*6.28))/160,
							60*sin(i/36*6.28)/160,
							0);
					}
				}
				if(t>300)
				{state=0;t=0;}
			}
			if(hp<400)
			{flag=6;t=0;loop=0;state=0;}
		}
		else if(flag==6)
		{
			tx+=(-50-tx)/60;ty-=ty/60;
			if(t>120)
			{flag=7;t=0;ct=0;}
		}
		else if(flag==7)
		{
			if(t%50==0)
			{
				var i;
				for(i=0;i<300;i+=1)
				{
					var sx,sy;
					sx=(sin(i)/3+1)*cos(i/-10);
					sy=(sin(i)/3+1)*sin(i/-10);
					Shot(103,
						x+tx+20,
						y+ty+40,
						sx/t*30,
						sy/t*30,
						16*atan2(sy,sx)/6.28+8);
				}
			}
		}
		if(hp<=0)
		{
			PlaySound(0,0);
			ShotToItem();
			var i;
			for(i=0;i<10;i+=1)
				Explode(111,
					x+20+50*cos(i/10*6),
					y+40+50*sin(i/10*6),
					1,110);
		}
		DrawImage(11,100,x+tx,y+ty);
	}
};
//midle boss
class BOSS1
{
	//flag	0:start , 1:normal , 2:end
	var t,flag,t2;
	var tx,ty;
	var tv;
	BOSS1()		//constructer
	{
		t=0;flag=0;t2=0;
		tx=-50;ty=0;tv=0;
		img=10; hp=350;
	}
	main()		//main rutin
	{
		t2+=1;
		t+=1;
		if(flag==0)
		{
			x-=3;
			if(t>20)
				flag=1;
		}
		else if(flag==1)
		{
			if(t%250==0)
			{
				PlaySound(1,0);
				var i;
				for(i=0;i<300;i+=1)
				{
					var sx,sy;
					sx=(sin(i)/3+1)*cos(i/-10);
					sy=(sin(i)/3+1)*sin(i/-10);
					Shot(103,
						x+tx+20,
						y+ty+40,
						sx,
						sy,
						16*atan2(sy,sx)/6.28+8);
				}
			}

			if(hp<200)
			{
				t=0;
				flag=3;
				PlaySound(0,0);
				ShotToItem();
				var i;
				for(i=0;i<10;i+=1)
					Explode(111,
						x+20+50*cos(i/10*6),
						y+40+50*sin(i/10*6),
						1,110);
			}
		}
		else if(flag==3)
		{
			if(t>200)
			{
				flag+=1;
				t=0;
			}
		}
		else if(flag==4)
		{
			if(t%400==0) PlaySound(3,0);
			if(t%400<80)
			{
				tv+=1;
				Shot(101,x+tx+20,y+ty+40,0.5*cos(t/2.5),0.5*sin(t/2.5),0);
				Shot(101,x+tx+20,y+ty+40,0.5*cos(t/-2.5),0.5*sin(t/-2.5),0);
				Shot(101,x+tx+20,y+ty+40,0.5*cos(t/2.5+3),0.5*sin(t/2.5+3),0);
				Shot(101,x+tx+20,y+ty+40,0.5*cos(t/-2.5+3),0.5*sin(t/-2.5+3),0);
			}
			ty=100*sin(tv/30);
		}
		else if(flag==5)
			x+=3;
		if(flag!=5 && (hp<30||t2>1800))
		{
			flag=5;
			PlaySound(0,0);
			ShotToItem();
			var i;
			for(i=0;i<10;i+=1)
				Explode(111,
					x+20+50*cos(i/10*6),
					y+40+50*sin(i/10*6),
					1,110);
		}
		DrawImage(11,100,x+tx,y+ty);
	}
};
//sin curve
class E0
{
	var t;
	E0(){t=0;img=0;hp=2;}
	main()
	{
		t+=1;
		x-=2;
		y+=cos(t/5);
		anm=t/3%6;
	}
};
//random shot
class E1
{
	var t,v;
	E1(){t=0;img=1;hp=15;v = (y-120+16)/abs(y-120+16)*(-1);}
	main()
	{
		t+=1;
		if(t<30)
			x -= 2;
		else if(t>270)
			x += 2;
		else if(t%120<20)
			v = (y-120+16)/abs(y-120+16)*(-1);
		else if(t%120<60)
		{
			y += v;
			if(t%10==0)
				Shot(101,x+16,y+16,-1,(rnd()-0.5)*2,0);
		}
		else if(t%120<80)
			v = (y-120+16)/abs(y-120+16)*(-1);
		else if(t%120<120)
		{
			y += v;
			if(t%10==0)
				Shot(101,x+16,y+16,-1,(rnd()-0.5)*2,0);
		}
	}
};
//cross shot
class E2
{
	var t,v;
	E2(){img=1;hp=30;t=0;v=(y-120)/abs(y-120)*(-1);}
	main()
	{
		t+=1;
		if(t%60<20)
			x -= 1;
		else if(t%60<30)
		{
			if(t%120<30 && t%10 && t<500)
			{
				Shot(101,x+16,y+16,-1.5,0,0);
				Shot(101,x+16,y+16,+1.5,0,0);
				Shot(101,x+16,y+16,0,+1.5,0);
				Shot(101,x+16,y+16,0,-1.5,0);
			}
		}
		else if(t%60<50)
			y += v;
		else{}
	}
};
//myship direction 1way shot
class E3
{
	var t,v,a;
	E3(){img=2;hp=14;t=0;v=2*(y-120)/abs(y-120)*(-1);a=v/-100;}
	main()
	{
		var k,i;
		t+=1;
		x-=0.2;
		v+=a;
		y+=v;
		k=atan2(py-y,px-x);
		anm=16*k/6.28+8;
		if(t%30==0 && t>30 && t<120)
			for(i=1;i<6;i+=1)
				Shot(101,
					x+16,
					y+16,
					cos(k)*i/2,
					sin(k)*i/2,
					0);
	}
};
//myship direction 2way shot
class E4
{
	var t,v;
	E4(){img=2;hp=14;t=0;v=(y-120)/abs(y-120)*(-1);}
	main()
	{
		var k,i;
		t+=1;
		x-=0.2;
		y+=v;
		k=atan2(py-y,px-x);
		anm=16*k/6.28+8;
		if(t%30==0 && t>30 && t<120)
		{
			k=atan2(py-y+50,px-x);
			for(i=1;i<6;i+=1)
			{
				Shot(102,
					x+16,
					y+16,
					cos(k)*i/2,
					sin(k)*i/2,
					0);
			}
			k=atan2(py-y-50,px-x);
			for(i=1;i<6;i+=1)
			{
				Shot(102,
					x+16,
					y+16,
					cos(k)*i/2,
					sin(k)*i/2,
					0);
			}
		}
	}
};
//45diag shot
class E10
{
	var t;
	E10(){t=0;img=3;hp=2;anm=(y<120);}
	main()
	{
		t+=1;
		x-=1;
		if(t%40==0 && t<100)
			Shot(100,x+8,y+8,-1.5,-1*(y-120)/abs(y-120),0);
	}
};
//-45diag shot
class E11
{
	var t;
	E11(){t=0;img=3;hp=2;anm=2+(y<120);}
	main()
	{
		t+=1;
		x-=1;
		if(t%40==0 && t<100)
			Shot(100,x+8,y+8,1,-1*(y-120)/abs(y-120),0);
	}
};
//up bug
class E12
{
	var t,f,shot,v;
	E12(){img=4;hp=2;t=0;f=0;shot=0;v=-2*(x-160)/abs(x-160);}
	main()
	{
		t+=1;
		if(f==0)
		{
			x+=v;
			anm=4+t/5%4;
			if(x>280&&x-10<280)
				f=1;
		}
		else if(f==1)
		{
			if(shot==0&&y>py)
			{
				var s;
				shot=1;
				s=sqrt((px-x)*(px-x)+(py-y)*(py-y));
				Shot(100,x+8,y+8,1.5*(px-x)/s,1.5*(py-y)/s,0);
			}
			y+=2;
			anm=0;
			if(Tikei(x,y+16)||Tikei(x+16,y+16))
				f=2;
		}
		else if(f==2)
		{
			x-=2;
			anm=t/5%4;
			if(Tikei(x,y+16)==0&&Tikei(x+16,y+16)==0)
				f=3;
		}
		else if(f==3)
		{
			y+=2;
			x-=1;
			anm=12+t/5%4;
			if(Tikei(x,y+16)||Tikei(x+16,y+16))
				f=2;
		}
	}
};
//floor bug
class E13
{
	var t,f,v;
	E13(){img=4;hp=2;t=0;f=2;v=-2*(x-160)/abs(x-160);}
	main()
	{
		t+=1;
		if(f==2)
		{
			x+=v;
			anm=t/5%4;
			if(Tikei(x,y+16)==0&&Tikei(x+16,y+16)==0)
				f=3;
		}
		else if(f==3)
		{
			y+=2;
			x-=1;
			anm=12+t/5%4;
			if(Tikei(x,y+16)||Tikei(x+16,y+16))
				f=2;
		}
	}
};
//round bug
class E14
{
	var t,f;
	E14(){img=4;hp=2;t=0;f=0;}
	main()
	{
		t+=1;
		if(f==0)
		{
			x-=2;
			anm=t/5%4;
			if(Tikei(x,y+17)==0&&Tikei(x+16,y+17)==0)
			{f=1;t=0;}
		}
		else if(f==1)
		{
			y+=2;
			x-=1;
			anm=12+t/5%4;
			if(Tikei(x+17,y)==0&&Tikei(x+17,y+16)==0)
			{f=2;t=0;}
		}
		else if(f==2)
		{
			x+=2;
			anm=4+t/5%4;
			if(Tikei(x,y-1)==0&&Tikei(x+16,y-1)==0)
			{f=3;t=0;}
		}
		else if(f==3)
		{
			y-=2;
			x-=1;
			anm=8+t/5%4;
			if(Tikei(x-1,y)==0&&Tikei(x-1,y+16)==0)
			{f=4;}
		}
		else if(f==4)	//falling
		{
			y+=2;
			anm=0;
			if(Tikei(x,y+16)||Tikei(x+16,y+16))
				f=5;
		}
		else if(f==5)	//floor
		{
			x+=2;
			anm=t/5%4;
			if(Tikei(x,y+16)==0&&Tikei(x+16,y+16)==0)
				f=4;
		}
	}
};
//shutter
class E20
{
	E20()
	{
		img=5;hp=-100;
	}
	main()
	{
		if(py<120)	y=20;
		else	y=140;
		x-=1;
		if(x<60)
			hp=0;
	}
};
//round shot
class E30
{
	var t,vx,vy;
	E30()
	{img=2;hp=30;t=0;vy=-0.2*(y-120)/abs(y-120);vx=-1;}
	main()
	{
		t+=1;t=t%60;
		vx+=0.006;
		x+=vx;
		y+=vy;
		anm=16*t/60;
		if(x<245)
			Shot(100,x+16,y+16,-1*cos(6.28*t/60),-1*sin(6.28*t/60),0);
	}
};
//fall bug
class E40
{
	var t,f,v,r;
	E40(){img=4;hp=2;t=0;f=0;v=-2*(x-160)/abs(x-160);r=random(50,300);}
	main()
	{
		t+=1;
		if(f==0)	//tenjyo
		{
			x+=v;
			anm=4+t/5%4;
			if(x>r&&x-10<r)
				f=1;
		}
		else if(f==1)	//falling
		{
			y+=2;
			anm=0;
			if(Tikei(x,y+16)||Tikei(x+16,y+16))
			{
				f=2;
				v=random(0,2)*4-2;
			}
		}
		else if(f==2)	//floor
		{
			x+=v;
			anm=t/5%4;
			if(Tikei(x,y+16)==0&&Tikei(x+16,y+16)==0)
				f=1;
		}
	}
};
//fall to myship bug
class E41
{
	var t,f,v;
	E41(){img=4;hp=2;t=0;f=0;v=-2*(x-160)/abs(x-160);}
	main()
	{
		t+=1;
		if(f==0)	//tenjyo
		{
			x+=v;
			anm=4+t/5%4;
			if(x>px+16&&x-2<=px+16)
				f=1;
		}
		else if(f==1)	//falling
		{
			y+=2;
			anm=0;
			if(Tikei(x,y+16)||Tikei(x+16,y+16))
				f=2;
		}
		else if(f==2)	//floor
		{
			x-=2;
			anm=t/5%4;
			if(Tikei(x,y+16)==0&&Tikei(x+16,y+16)==0)
				f=1;
		}
	}
};
//myship direction shot
class E42
{
	var t;
	E42(){t=0;img=3;hp=5;anm=(y<120);}
	main()
	{
		var k;
		t+=1;
		x-=1;
		k=atan2(py-y,px-x);
		if(t%5==0 && t<160)
			Shot(101,x+16,y+16,4*cos(k),4*sin(k),0);
	}
};
//yoko shot
class E43
{
	var t,f,v;
	E43(){t=0;img=1;hp=20;f=0;}
	main()
	{
		t+=1;
		if(f==0)
		{
			x-=2;
			if(t>30)
			{f=1;t=0;}
		}
		else if(f==1)
		{
			var i;
			if(t==10)
				for(i=-3;i<=3;i+=1)
					Shot(102,x+5,y+16+i*8,-2,0,0);
			if(t>20)
			{f=2;t=0;v=random(0,2)*2-1;}
		}
		else if(f==2)
		{
			y+=v;
			if(t>30)
			{f=0;t=0;}
		}
	}
};
//Stage Initializes
Init()
{
	LoadImage2(0,"img\\enemy0.png",16,16);
	LoadImage(1,"img\\enemy1.png");
	LoadImage2(2,"img\\enemy2.png",32,32);
	LoadImage2(3,"img\\enemy3.png",16,16);
	LoadImage2(4,"img\\enemy4.png",16,16);
	LoadImage(5,"img\\enemy5.png");
	LoadImage(10,"img\\futaba.png");
	LoadImage(11,"img\\tadakiti.png");

	LoadImage(100,"img\\enemyshot1.png");
	LoadImage(101,"img\\enemyshot2.png");
	LoadImage(102,"img\\enemyshot3.png");
	LoadImage2(103,"img\\enemyshot20.png",12,12);
	LoadImage2(104,"img\\enemyshot21.png",8,8);
	LoadImage(105,"img\\lshot1.png");

	LoadImage2(110,"img\\explode1.png",160,160);
	LoadImage2(111,"img\\explode2.png",64,64);

	LoadSound(0,"snd\\explode.wav");
	LoadSound(1,"snd\\da.wav");
	LoadSound(2,"snd\\da2.wav");
	LoadSound(3,"snd\\da4.wav");
}